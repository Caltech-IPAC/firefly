ext {

  apache_lib = [':commons-fileupload',
                     ':commons-io-2.4',
                     ':commons-httpclient',
                     ':commons-codec',
                     ':commons-lang',
                     ':commons-logging',
                     ':jackrabbit-webdav-2.8.0',
                     ':commons-discovery',
                     ':commons-csv-1.0-201209']

  webservices_lib = [':archive_dto_irsa',
                     ':axis',
                     ':archive_dto_votable',
                     ':xbean']

  ehcache_lib = [':ehcache-2.7.4',
                 ':slf4j-api-1.6.6',
                 ':ehcache-web-2.0.4']

  j2ee_rt_lib = [':wsdl4j-1.5.1',
                 ':jaxrpc',
                 ':saaj',
                 ':activation',
                 ':mail']

  j2ee_lib = [j2ee_rt_lib,
              ':servlet-api',
              ':websocket-api']

  mysql_lib = ':mysql-connector-java-5.1.6-bin'
  starlink_lib = [':rayreg', ':starlink-registry', ':jcdf', ':eag-healpix', ':ndx', ':mirage', ':gbin', ':vecmath', ':commons-compress', ':starlink-pal', ':starlink-cdf', ':starlink-connect', ':starlink-fits', ':starlink-table', ':starlink-util', ':starlink-votable']
  xbeanfinder_lib = [':xbean-finder-4.1', ':asm-5.0.3']
  spring_lib = [':spring-beans', ':spring-core', ':spring-jdbc', ':spring-tx', ':spring-web']
  fits_io_lib = [':fits_io']
  junit_lib = [':junit-4.12', ':hamcrest-core-1.3']
  log4j_lib = ':log4j-1.2.14'
  josso_lib = ':josso-ws-1.8.5'
  oidc_lib = [':nimbus-jose-jwt-4.37.1', ':json-smart-2.3', ':accessors-smart-1.2' ]
  json_lib = ':json-simple-1.1.1'
  xstream_lib = [':xstream-1.3.1', ':xpp3_min-1.1.4c']
  gwt_lib_rt = [':gwt-servlet', ':gwt-servlet-deps']
  gwt_lib = [ gwt_lib_rt,
             ':gwt-user',
             ':gwt-dev',
             ':gwt-codeserver',
             ':validation-api-1.0.0.GA',
             ':validation-api-1.0.0.GA-sources',
             ':canvas-2.1-r1766',
             ':gwt-incubator-2.1-r1766-striped',
             ':gchart' ]

  firefly_ext_lib = [ ':xstream-gwt', ':simbad_ws' ]

  firefly_lib = [ firefly_ext_lib, ':firefly' ]


  FIREFLY_RUNTIME = [':firefly_data'] + fits_io_lib + apache_lib + spring_lib + webservices_lib + oidc_lib +
                     log4j_lib + josso_lib + xstream_lib + xbeanfinder_lib +
                     gwt_lib_rt + ehcache_lib + starlink_lib + j2ee_rt_lib + json_lib

  FIREFLY_COMPILE = j2ee_lib + gwt_lib + FIREFLY_RUNTIME
}


dependencies {
  compile fits_io_lib
  compile FIREFLY_COMPILE
  testCompile junit_lib
}

def libPath = "$fireflyPath/jars"

//project.ext.properties.each{ k, v -> println "${k}:${v}\n" }

repositories {
  flatDir {
    name 'firefly_repo'
    dirs  "$libPath/build",
          "$libPath/asm",
          "$libPath/data",
          "$libPath/ehcache",
          "$libPath/fits-io",
          "$libPath/j2ee",
          "$libPath/gwt",
          "$libPath/gwt-incubator",
          "$libPath/jakarta-commons",
          "$libPath/josso",
          "$libPath/oidc",
          "$libPath/json",
          "$libPath/log4j",
          "$libPath/mysql",
          "$libPath/sdss",
          "$libPath/spring",
          "$libPath/starlink",
          "$libPath/test",
          "$libPath/webservices",
          "$libPath/xbean",
          "$libPath/xstream"
  }
}

jar.destinationDir = file ("$rootDir/jars/build")

clean {
  delete "${jar.destinationDir}/${jar.baseName}.jar"
}

task buildClient (dependsOn: loadConfig) {
  description= 'Build JavaScript portion of the application.'
  group = MISC_GROUP

  outputs.dir "${buildDir}/war"
  inputs.dir "${projectDir}/js"
  inputs.dir "${fireflyPath}/src/firefly/js"

  doLast {
    if (!file("webpack.config.js").exists()) {
      println ">> webpack.config.js not found.  Skipping buildClient."
      throw new StopExecutionException("webpack.config.js not found.  Skipping buildClient.")
    }
    def res = project.ext.NODE 'yarn', 'run', 'build'
    if (res.getExitValue() != 0) {
      throw new GradleException("Fail to build Client")
    }
  }
}

task buildJsDoc (dependsOn: loadConfig) {
  description= 'Build JSDoc.'
  group = MISC_GROUP

  def outdir = "${buildDir}/war"

  outputs.dir outdir
  inputs.dir "${projectDir}"
  inputs.dir "${fireflyPath}/docs"
  inputs.dir "${fireflyPath}/src/firefly"

  doLast {
    if (!file("jsdoc_config.json").exists()) {
      println ">> jsdoc_config.json not found.  Skipping buildJsDoc."
      throw new StopExecutionException("jsdoc_config.json not found.  Skipping buildJsDoc.")
    }
    def res = project.ext.NODE "${fireflyPath}/node_modules/.bin/jsdoc", '-c', 'jsdoc_config.json', '-d', "${outdir}/docs/js"
    if (res.getExitValue() != 0) {
      throw new GradleException("Fail to build Client")
    }
  }
}

task publishJsDocs (dependsOn: loadConfig) {
  description = 'Publish js docs to firefly.lsst.io'
  group = MISC_GROUP

  def docsDir = "${buildDir}/war/docs/js"
  doLast {
    def res = project.ext.publishDocs docsDir, 'firefly'
    if (res.getExitValue() != 0) {
      throw new GradleException("Fail to publishJsDoc")
    }
  }
}

task publishPythonDocs (dependsOn: loadConfig) {
  description = 'Publish python docs to firefly_client.lsst.io'
  group = MISC_GROUP

  def docsDir = "${buildDir}/war/docs/python"
  doLast {
    def res = project.ext.publishDocs docsDir, 'firefly_client'
    if (res.getExitValue() != 0) {
      throw new GradleException("Fail to publishJsDoc")
    }
  }
}


task dockerImage (dependsOn: loadConfig) {
  description = 'Create a docker image'
  group = MISC_GROUP

  ext.docker_repo = "ipac/firefly"
  ext.docker_registry = ''
  ext.docker_tag = 'latest'
  ext.copy_res = true

  doLast {
    // copy artifacts to staging directory
    if (copy_res) {
      copy {
        from ("${project.distDir}") { include '*.war' }
        from ("${fireflyPath}/docker/base") { include '*' }
        into "${buildDir}/docker"
      }
    }

    try {
      "docker --version".execute()
    } catch (Exception e) {
      println ">> docker is not installed.  This task required docker"
      throw new GradleException("docker is not installed.  This task required docker", e)
    }

    if (!file("${buildDir}/docker/Dockerfile").exists()) {
      println ">> Dockerfile not found. Fail to create docker image"
      throw new GradleException("Dockerfile not found. Fail to create docker image")
    }

    docker_repo = project.appConfigProps.docker_repo ?: docker_repo
    docker_tag = project.appConfigProps.docker_tag ?: docker_tag
    docker_registry = project.appConfigProps.docker_registry ?: docker_registry
    docker_registry = docker_registry == '' || docker_registry.endsWith('/') ? docker_registry : docker_registry + '/'

    def res = exec {
      workingDir "${buildDir}/docker"
      commandLine "docker build -t ${docker_registry}${docker_repo}:${docker_tag} --build-arg IMAGE_NAME=${docker_registry}${docker_repo} .".split(' ')
    }
    if (res.getExitValue() != 0) {
      throw new GradleException("Fail to create docker image")
    }
    return res;
  }
}

task dockerPublish (dependsOn: dockerImage) {
  description = 'Create a docker image'
  group = MISC_GROUP

  doLast {

    try {
      "docker --version".execute()
    } catch (Exception e) {
      println ">> docker is not installed.  This task required docker"
      throw new GradleException("docker is not installed.  This task required docker", e)
    }

    if (project.appConfigProps.docker_user != '') {
      def proc = "docker login --username ${project.appConfigProps.docker_user} --password ${project.appConfigProps.docker_passwd}".execute()
      proc.waitForOrKill(10000)
      println ">> docker login as ${project.appConfigProps.docker_user} with exit status ${proc.exitValue()}"
    }

    def docker_repo = project.appConfigProps.docker_repo ?: dockerImage.docker_repo
    def docker_tag = project.appConfigProps.docker_tag ?: dockerImage.docker_tag
    def docker_registry = project.appConfigProps.docker_registry ?: dockerImage.docker_registry
    docker_registry = docker_registry == '' || docker_registry.endsWith('/') ? docker_registry : docker_registry + '/'

    def res = exec {
      workingDir "${buildDir}/docker"
      commandLine "docker push ${docker_registry}${docker_repo}:${docker_tag}".split(' ')
    }
    if (res.getExitValue() != 0) {
      throw new GradleException("Fail to push docker image")
    }
    return res;
  }
}

/**
 * this function setup node.js environment then run the given command.
 */
ext.NODE = { ...cmd ->
  def wpBuildDir = "${buildDir}/war"
  def tag = "v" + getVersionTag() + ' Built On:' + build_time;

  try {
    def process = "yarn version".execute()
  } catch (Exception e) {
    println ">> Task failed due to missing yarn package manager which comes bundled with node.js"
    throw new GradleException("Task failed due to missing yarn package manager which comes bundled with node.js", e)
  }

  if (file("${rootDir}/package.json").exists()) {
    exec {
      workingDir = rootDir
      executable "yarn"
      args "install", "--frozen-lockfile"
    }
  }

  // any environment starting with '__$' will be defined as global.
  // this is a way send config info to JS code.
  def res = exec {
    environment '__$version_tag': tag
    environment '__$help_base_url': project.appConfigProps['help.base.url']
    environment 'WP_BUILD_DIR': wpBuildDir
    environment 'NODE_ENV': (project.env == 'local' ? 'development' : 'production')
    commandLine cmd
    for (String key : project.appConfigProps.keySet()) {
      if (key.startsWith('__$')) {
        environment (key, project.appConfigProps[key])
        println ">>   " + key + " = " + project.appConfigProps[key]
      }
    }

  }
  return res;
}

/**
 * Publish docs to lsst.io
 */
ext.publishDocs = { docsDir, product ->

  def branch = System.env.BRANCH ?: "master"
  docsDir = docsDir ?: "${buildDir}/gwt/${project['app-name']}/docs"
  product = product ?: "firefly"

  if (!file(docsDir).exists()) {
    println ">> docs directory does not exists.  Skipping publishDocs."
    throw new StopExecutionException("docs directory does not exists.  Skipping publishDocs.")
  }

  try {
    "ltd-mason-travis -h".execute()
  } catch (Exception e) {
    println ">> ltd-mason-travis is not installed.  Task cannot continue."
    throw new GradleException("ltd-mason-travis is not installed.  Task cannot continue.\"", e)
  }

  def res = exec {
              environment 'LTD_MASON_BUILD': 'true'
              environment 'LTD_MASON_PRODUCT': product
              environment 'LTD_MASON_AWS_ID': 'AKIAJRVDEID2GE6GLL7Q'
              environment 'LTD_MASON_AWS_SECRET': project.appConfigProps['LTD_MASON_AWS_SECRET']
              environment 'LTD_KEEPER_URL': 'https://keeper.lsst.codes'
              environment 'LTD_KEEPER_USER': 'firefly'
              environment 'LTD_KEEPER_PASSWORD': project.appConfigProps['LTD_KEEPER_PASSWORD']
              environment 'TRAVIS_PULL_REQUEST': 'false'
              environment 'TRAVIS_REPO_SLUG': 'Caltech-IPAC/firefly'
              environment 'TRAVIS_BRANCH': branch
              commandLine "ltd-mason-travis", "--html-dir", docsDir
            }
  return res
}
